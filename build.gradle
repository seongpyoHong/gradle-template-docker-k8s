//plugins
plugins {
    id 'org.springframework.boot' version '2.2.5.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id 'com.google.cloud.tools.jib' version '2.1.0' apply false
}

subprojects {
    //project information
    group = 'com.sphong'
    version = '0.0.1-SNAPSHOT'
    //java version
    sourceCompatibility = '1.8'

    apply plugin : 'java'
    apply plugin : 'org.springframework.boot'
    apply plugin : 'io.spring.dependency-management'
    apply plugin : 'com.google.cloud.tools.jib'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    //repository looking for dependencies
    repositories {
        mavenCentral()
        //jcenter()
    }

    //global variable
    ext {
        set('springCloudVersion', "Hoxton.SR1")
        set('gcr',"asia.gcr.io")
        set('projectName',"sphong-kuber")
    }

//Add dependencies
    dependencies {
        implementation 'org.springframework.boot:spring-boot-actuator'
        implementation 'org.springframework.boot:spring-boot-starter-webflux'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.cloud:spring-cloud-starter-netflix-ribbon'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }

//Setting for dependency management
    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    test {
        useJUnitPlatform()
    }

    //build docker image
    jib {
        to {
            image = "${gcr}/${projectName}/" + "${bootJar.baseName}:${bootJar.version}"
            tags = ['latest']
        }
    }

}

project(':sample-api') {
    dependencies {
        compile project(':sample-common')
    }
    //Customizing bootJar
    bootJar {
        baseName = "${project.name}"
        version =  '0.1.0'
    }

}

project(':sample-admin') {
    //Customizing bootJar
    bootJar {
        baseName = "${project.name}"
        version =  '0.1.0'
    }

    dependencies {
        compile project(':sample-common')
    }
}

project(':sample-common') {
    bootJar {
        baseName = "${project.name}"
        version =  '0.1.0'
    }
}